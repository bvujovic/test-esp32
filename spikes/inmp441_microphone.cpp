/*
  ESP32 I2S Microphone Sample
  esp32-i2s-mic-sample.ino
  Sample sound from I2S microphone, display on Serial Plotter
  Requires INMP441 I2S microphone
  DroneBot Workshop 2022
  https://dronebotworkshop.com
*/

// https://marketplace.visualstudio.com/items?itemName=alexnesnes.teleplot
// https://docs.espressif.com/projects/esp-idf/en/v4.2.3/esp32/api-reference/peripherals/i2s.html#_CPPv48i2s_read10i2s_port_tPv6size_tP6size_t10TickType_t
// https://github.com/espressif/esp-idf/issues/4015
// https://github.com/pschatzmann/arduino-audio-tools

#include <Arduino.h>
// Include I2S driver
#include <driver/i2s.h>

// https://docs.adeus.ai/guides/esp32-c3.html
// Connections to INMP441 I2S microphone
// pins for ESP32-C3
#define I2S_WS 0
#define I2S_SD 2
#define I2S_SCK 1

// Use I2S Processor 0
#define I2S_PORT I2S_NUM_0

// Define input buffer length
#define bufferLen 64
int16_t sBuffer[bufferLen];

void i2s_install()
{
    // Set up I2S Processor configuration
    const i2s_config_t i2s_config = {
        .mode = i2s_mode_t(I2S_MODE_MASTER | I2S_MODE_RX),
        .sample_rate = 44100,
        .bits_per_sample = i2s_bits_per_sample_t(16),
        .channel_format = I2S_CHANNEL_FMT_ONLY_LEFT,
        .communication_format = i2s_comm_format_t(I2S_COMM_FORMAT_STAND_I2S),
        .intr_alloc_flags = 0,
        .dma_buf_count = 8,
        .dma_buf_len = bufferLen,
        .use_apll = false};

    auto res = i2s_driver_install(I2S_PORT, &i2s_config, 0, NULL);
    Serial.println("i2s_driver_install result: " + String(res));
}

void i2s_setpin()
{
    // Set I2S pin configuration
    const i2s_pin_config_t pin_config = {
        .bck_io_num = I2S_SCK,
        .ws_io_num = I2S_WS,
        .data_out_num = -1,
        .data_in_num = I2S_SD};

    auto res = i2s_set_pin(I2S_PORT, &pin_config);
    Serial.println("i2s_set_pin result: " + String(res));
}

ulong ms;

void setup()
{
    // Set up Serial Monitor
    Serial.begin(115200);
    Serial.println();

    delay(1000);

    // Set up I2S
    i2s_install();
    i2s_setpin();
    auto res = i2s_start(I2S_PORT);
    Serial.println("i2s_start result: " + String(res));

    delay(500);
    ms = 0;
}

ulong msLastPrint = 0;

void loop()
{
    // if (millis() - ms < 10)
    //     return;
    // ms = millis();

    // False print statements to "lock range" on serial plotter display
    // Change rangelimit value to adjust "sensitivity"
    // int rangelimit = 3000;
    // Serial.print(rangelimit * -1);
    // Serial.print(" ");
    // Serial.print(rangelimit);
    // Serial.print(" ");

    // Get I2S data and place in data buffer
    size_t bytesIn = 0;
    esp_err_t result = i2s_read(I2S_PORT, &sBuffer, bufferLen, &bytesIn, portMAX_DELAY);

    if (result == ESP_OK)
    {
        // Read I2S data buffer
        int16_t samples_read = bytesIn / 2;
        // Serial.print("Bytes read: " + String(bytesIn));
        // Serial.println(" Samples read: " + String(samples_read));
        if (samples_read > 0)
        {
            auto sum = 0;
            for (int16_t i = 0; i < samples_read; ++i)
                sum += (sBuffer[i]);
            auto mean = sum / samples_read;
            if (mean > 50 || mean < -50)
                Serial.println(mean);
        }
    }
    //* Testing if Serial monitor is alive - prints a dot every 5 seconds
    if (millis() - msLastPrint > 5000)
    {
        Serial.print('.');
        msLastPrint = millis();
    }
}
